%%% PMAP_D2D, a privacy-preserving mutual authentication and key agreement protocol for internet of drones 
%%% between drone and drone
%%% 1. A -> Z : {A,Z,Na}_CRPa, {A,Z,Na,B}_CRPa
%%% 2. Z -> A : {A,Z,B,Na,Nz}_CRPa
%%% 3. A -> Z : {A,Z,B,Nz,Na2}_CRPa, {A,Z,B,Nz,Na2,Ra}_CRPa
%%% 4. Z -> B : {B,Z,Nz2}_CRPb, {B,Z,Nz2,Na2}_CRPb, {B,Z,Nz2,Na2,A}_CRPb
%%% 5. B -> Z : {B,Z,A,Nz2,Nb}_CRPb, {B,Z,A,Nz2,Nb,Rb}_CRPb
%%% 6. Z -> A : {A,Z,B,Na2,Nb}_CRPa

role role_A(A:agent,B:agent,Z:agent,CRP:symmetric_key,Hash:hash_func,SND,RCV:channel(dy))
played_by A
def=
	local
		State:nat,
		Na,Na2,Nz,Nb:text,
		MAC12,MAC3,MAC45,MAC11,Ca,Ra,K:message
	init
		State := 0
	transition
		1. State=0 /\ RCV(start) =|> 
		State':=2 /\ Na':=new() /\ MAC12':= Hash({A.Z.Na'}_CRP.{A.Z.Na'.B}_CRP.Na'.B)
		/\ SND({A.Z.Na'}_CRP.{A.Z.Na'.B}_CRP.MAC12')

		2. State=2 /\ RCV({A.Z.B.Na.Nz'}_CRP.MAC3') =|>
		State':=6 /\ Na2':=new() /\ Ca':={Nz'.Na2'}_CRP /\ Ra':=Hash(Ca')
		/\ MAC45':= Hash({A.Z.B.Nz'.Na2'}_CRP.{A.Z.B.Nz'.Na2'.Ra'}_CRP.Na2'.Ra')
		/\ SND({A.Z.B.Nz'.Na2'}_CRP.{A.Z.B.Nz'.Na2'.Ra'}_CRP.MAC45')
		/\ secret(Na2',sec_2,{A,B,Z}) /\ secret(Ra',sec_6,{A,Z})

		/\ request(A,Z,auth_1,Na)
		/\ witness(A,Z,auth_2,Nz')

		3. State=6 /\ RCV({A.Z.B.Na2.Nb'}_CRP.MAC11') =|>
		State':=9 /\ K':= xor(Hash(Na2),Hash(Nb'))

		/\ request(A,Z,auth_4,Na2)
end role

role role_Z(A:agent,B:agent,Z:agent,CRP:symmetric_key,CRPb:symmetric_key,Hash:hash_func,SND,RCV:channel(dy))
played_by Z
def=
	local
		State:nat,
		Na,Na2,Nz,Nz2,Nb:text,
		MAC12,MAC3,MAC45,MAC678,MAC910,MAC11,Ca,Ra,Cb,Rb:message
	init
		State := 1
	transition
		1. State=1 /\ RCV({A.Z.Na'}_CRP.{A.Z.Na'.B}_CRP.MAC12') =|>
		State':=3 /\ Nz':=new() /\ Nz2':=new() /\ MAC3':=Hash({A.Z.B.Na'.Nz'}_CRP.Na'.Nz')
		/\ SND({A.Z.B.Na'.Nz'}_CRP.MAC3')
		/\ secret(Nz',sec_1,{A,Z})

		/\ witness(Z,A,auth_1,Na')

		2. State=3 /\ RCV({A.Z.B.Nz.Na2'}_CRP.{A.Z.B.Nz.Na2'.Ra'}_CRP.MAC45') =|>
		State':=5 /\ MAC678':=Hash({B.Z.Nz2}_CRPb.{B.Z.Nz2.Na2'}_CRPb.{B.Z.Nz2.Na2'.A}_CRPb.Nz2.Na2'.A)
		/\ SND({B.Z.Nz2}_CRPb.{B.Z.Nz2.Na2'}_CRPb.{B.Z.Nz2.Na2'.A}_CRPb.MAC678')
		/\ secret(Nz2,sec_3,{B,Z})

		/\ request(Z,A,auth_2,Nz)
		
		3. State=5 /\ RCV({B.Z.A.Nz2.Nb'}_CRPb.{B.Z.A.Nz2.Nb'.Rb'}_CRPb.MAC910') =|>
		State':=7 /\ Cb':= {Nz2.Nb'}_CRPb
		/\ MAC11':= Hash({A.Z.B.Na2.Nb'}_CRP.Na2.Nb')
		/\ SND({A.Z.B.Na2.Nb'}_CRP.MAC11')

		/\ request(Z,B,auth_3,Nz2)
		/\ witness(Z,A,auth_4,Na2)
end role

role role_B(A:agent,B:agent,Z:agent,CRPb:symmetric_key,Hash:hash_func,SND,RCV:channel(dy))
played_by B
def=
	local
		State:nat,
		Na2,Nz2,Nb:text,
		MAC678,MAC910,Cb,Rb,K:message
	init
		State := 4
	transition
		1. State=4 /\ RCV({B.Z.Nz2'}_CRPb.{B.Z.Nz2'.Na2'}_CRPb.{B.Z.Nz2'.Na2'.A}_CRPb.MAC678') =|>
		State':=8 /\ Nb':=new() /\ Cb':={Nz2'.Nb'}_CRPb /\ Rb':=Hash(Cb')
		/\ MAC910':= Hash({B.Z.A.Nz2'.Nb'}_CRPb.{B.Z.A.Nz2'.Nb'.Rb'}_CRPb.Nb'.Rb')
		/\ K':= xor(Hash(Nb'),Hash(Na2'))
		/\ SND({B.Z.A.Nz2'.Nb'}_CRPb.{B.Z.A.Nz2'.Nb'.Rb'}_CRPb.MAC910')
		/\ secret(Nb',sec_4,{A,B,Z}) /\ secret(Rb',sec_5,{B,Z})

		/\ witness(B,Z,auth_3,Nz2')
end role

role session(A:agent,B:agent,Z:agent,CRP:symmetric_key,CRPb:symmetric_key,Hash:hash_func)
def=
	local
		SND3,RCV3,SND2,RCV2,SND1,RCV1:channel(dy)
	composition
		role_A(A,B,Z,CRP,Hash,SND1,RCV1) /\
		role_Z(A,B,Z,CRP,CRPb,Hash,SND2,RCV2) /\
		role_B(A,B,Z,CRPb,Hash,SND3,RCV3)
end role

role environment()
def=
	const
		drone1,drone2,groundstation:agent,
		crp,crpb:symmetric_key,
		h:hash_func,
		sec_1,sec_2,sec_3,sec_4,sec_5,sec_6,auth_1,auth_2,auth_3,auth_4:protocol_id
	intruder_knowledge = {drone1,drone2,groundstation,h}

	composition
		session(drone1,drone2,groundstation,crp,crpb,h)
		/\ session(drone1,drone2,groundstation,crp,crpb,h)
		%/\ session(i,drone2,groundstation,crp,crpb,h)
		%/\ session(drone1,i,groundstation,crp,crpb,h)
		%/\ session(drone1,drone2,i,crp,crpb,h)
end role

goal
	secrecy_of sec_1
	secrecy_of sec_2
	secrecy_of sec_3
	secrecy_of sec_4
	secrecy_of sec_5
	secrecy_of sec_6
	authentication_on auth_1
	authentication_on auth_2
	authentication_on auth_3
	authentication_on auth_4
end goal

environment()